apply plugin: "com.android.application"
apply plugin: "org.jetbrains.kotlin.android"
apply plugin: "com.facebook.react"



/**
 * This is the configuration block to customize your React Native Android app.
 * By default you don't need to apply any configuration, just uncomment the lines you need.
 */
react {
    /* Folders */
    //   The root of your project, i.e. where "package.json" lives. Default is '../..'
    // root = file("../../")
    //   The folder where the react-native NPM package is. Default is ../../node_modules/react-native
    // reactNativeDir = file("../../node_modules/react-native")
    //   The folder where the react-native Codegen package is. Default is ../../node_modules/@react-native/codegen
    // codegenDir = file("../../node_modules/@react-native/codegen")
    //   The cli.js file which is the React Native CLI entrypoint. Default is ../../node_modules/react-native/cli.js
    // cliFile = file("../../node_modules/react-native/cli.js")

    /* Variants */
    //   The list of variants to that are debuggable. For those we're going to
    //   skip the bundling of the JS bundle and the assets. By default is just 'debug'.
    //   If you add flavors like lite, prod, etc. you'll have to list your debuggableVariants.
    // debuggableVariants = ["liteDebug", "prodDebug"]

    /* Bundling */
    //   A list containing the node command and its flags. Default is just 'node'.
    // nodeExecutableAndArgs = ["node"]
    //
    //   The command to run when bundling. By default is 'bundle'
    // bundleCommand = "ram-bundle"
    //
    //   The path to the CLI configuration file. Default is empty.
    // bundleConfig = file(../rn-cli.config.js)
    //
    //   The name of the generated asset file containing your JS bundle
    // bundleAssetName = "MyApplication.android.bundle"
    //
    //   The entry file for bundle generation. Default is 'index.android.js' or 'index.js'
    // entryFile = file("../js/MyApplication.android.js")
    //
    //   A list of extra flags to pass to the 'bundle' commands.
    //   See https://github.com/react-native-community/cli/blob/main/docs/commands.md#bundle
    // extraPackagerArgs = []

    /* Hermes Commands */
    //   The hermes compiler command to run. By default it is 'hermesc'
    // hermesCommand = "$rootDir/my-custom-hermesc/bin/hermesc"
    //
    //   The list of flags to pass to the Hermes compiler. By default is "-O", "-output-source-map"
    // hermesFlags = ["-O", "-output-source-map"]

    /* Autolinking */
    autolinkLibrariesWithApp()
}

/**
 * Set this to true to Run Proguard on Release builds to minify the Java bytecode.
 */
def enableProguardInReleaseBuilds = true

/**
 * The preferred build flavor of JavaScriptCore (JSC)
 *
 * For example, to use the international variant, you can use:
 * `def jscFlavor = io.github.react-native-community:jsc-android-intl:2026004.+`
 *
 * The international variant includes ICU i18n library and necessary data
 * allowing to use e.g. `Date.toLocaleString` and `String.localeCompare` that
 * give correct results when using with locales other than en-US. Note that
 * this variant is about 6MiB larger per architecture than default.
 */
def jscFlavor = 'io.github.react-native-community:jsc-android:2026004.+'

android {
    ndkVersion rootProject.ext.ndkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion
    compileSdk rootProject.ext.compileSdkVersion

    assetPacks = [":sherpa_models"]

    namespace "com.xdcobra.voiceexample"
    defaultConfig {
        applicationId "com.xdcobra.voicelab"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        def resolvedVersionCode = project.findProperty('VERSION_CODE') ?: "1"
        def resolvedVersionName = project.findProperty('VERSION_NAME') ?: "0.0.1"
        versionCode resolvedVersionCode.toInteger()
        versionName resolvedVersionName.toString()
    }
    signingConfigs {
        debug {
            storeFile file('debug.keystore')
            storePassword 'android'
            keyAlias 'androiddebugkey'
            keyPassword 'android'
        }
        release {
            if (project.hasProperty('RELEASE_STORE_FILE')) {
                storeFile file(RELEASE_STORE_FILE)
            }
            if (project.hasProperty('RELEASE_STORE_PASSWORD')) {
                storePassword RELEASE_STORE_PASSWORD
            }
            if (project.hasProperty('RELEASE_KEY_ALIAS')) {
                keyAlias RELEASE_KEY_ALIAS
            }
            if (project.hasProperty('RELEASE_KEY_PASSWORD')) {
                keyPassword RELEASE_KEY_PASSWORD
            }
        }
    }
    buildTypes {
        debug {
            signingConfig signingConfigs.debug
        }
        release {
            // Caution! In production, you need to generate your own keystore file.
            // see https://reactnative.dev/docs/signed-apk-android.
            // Allow disabling release signing for local/CI via -PdisableReleaseSigning=true (uses debug key)
            if (project.hasProperty('disableReleaseSigning') && project.property('disableReleaseSigning') == 'true') {
                println "[build.gradle] Release signing disabled by -PdisableReleaseSigning=true, using debug key"
                signingConfig signingConfigs.debug
            } else {
                signingConfig signingConfigs.release
            }
            minifyEnabled enableProguardInReleaseBuilds
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
        }
    }
    // we use react-native-audio-api which has its own FFmpeg so we are facing a double linking issue
    // https://docs.swmansion.com/react-native-audio-api/docs/other/ffmpeg-info?platform=android
    // unfortunately, setting disableAudioapiFFmpeg=true does not work
    // as we dont use AudioAPI FFmpeg functions and our ffmpeg libaries are getting loaded first, we can use pickFirst for this example app
    packaging {
        pickFirst '**/libavcodec.so'
        pickFirst '**/libavformat.so'
        pickFirst '**/libavutil.so'
        pickFirst '**/libswresample.so'
        pickFirst '**/libswscale.so'
    }
}

task printJniLibs {
  doLast {
    println "jniLibs.srcDirs:"
    android.sourceSets.main.jniLibs.srcDirs.each { println it.absolutePath }
  }
}

dependencies {
    // The version of react-native is set by the React Native Gradle Plugin
    implementation("com.facebook.react:react-android")

    if (hermesEnabled.toBoolean()) {
        implementation("com.facebook.react:hermes-android")
    } else {
        implementation jscFlavor
    }
}

// Play Asset Delivery (PAD): models live in the :sherpa_models asset-pack only (not in main app assets).
// - assetPacks = [":sherpa_models"] makes the pack part of the app; include ':sherpa_models' in settings.gradle.
// - At runtime, the pack path is returned by getAssetPackPath("sherpa_models") so the app can list and load models.
// - Download models into the pack folder before build so the asset pack has content (local + CI).
def sherpaModelsOutputDir = file("${rootDir}/sherpa_models/src/main/assets/models")
def sherpaModelsCacheDir = file("${rootDir}/../.model-cache")
def sherpaModelsScript = file("${rootDir}/../scripts/download-models.js")
def sherpaModelsConfig = file("${rootDir}/../scripts/model-download-config.json")

tasks.register("downloadSherpaModels", Exec) {
    description = "Download SherpaOnnx models into the sherpa_models asset-pack"
    workingDir file("${rootDir}/..")
    commandLine "node",
        sherpaModelsScript.absolutePath,
        "--platform", "android",
        "--output", sherpaModelsOutputDir.absolutePath,
        "--cache", sherpaModelsCacheDir.absolutePath,
        "--config", sherpaModelsConfig.absolutePath
}

preBuild.dependsOn(tasks.named("downloadSherpaModels"))

// Play Asset Delivery (PAD) local testing: build AAB, then use bundletool to build APKs with
// --local-testing and install so getAssetPackPath("sherpa_models") works.
// Bundletool: in PATH, or set -Pbundletool=C:\path\to\bundletool (Windows) / -Pbundletool=/path/to/bundletool (Unix).
// See: https://developer.android.com/guide/playcore/asset-delivery/test
//
// Usage: ./gradlew installDebugWithPad

def getBundletoolCommandLine(List<String> args) {
    def exe = project.hasProperty("bundletool") ? project.property("bundletool").toString() : null
    if (exe == null || exe.isEmpty()) exe = "bundletool"
    def isWindows = org.gradle.internal.os.OperatingSystem.current().isWindows()
    // On Windows, Gradle often doesn't see PATH; run via cmd /c with one command string so the shell resolves bundletool
    if (isWindows && !new File(exe).exists()) {
        def quote = { String s -> (s.contains(" ") || s.contains("&")) ? "\"${s}\"" : s }
        def cmdStr = ([exe] + args).collect(quote).join(" ")
        return ["cmd", "/c", cmdStr]
    }
    return [exe] + args
}

tasks.register("buildApksForPad", Exec) {
    description = "Build APKs from debug AAB with --local-testing (for PAD)"
    group = "install"
    dependsOn tasks.named("bundleDebug")
    doFirst {
        def buildDir = layout.buildDirectory.get().asFile
        def aab = file("${buildDir}/outputs/bundle/debug/app-debug.aab")
        if (!aab.exists()) aab = file("${buildDir}/outputs/bundles/debug/app-debug.aab")
        if (!aab.exists()) throw new RuntimeException("AAB not found. Tried outputs/bundle/debug and outputs/bundles/debug.")
        def apksDir = file("${buildDir}/outputs/apks")
        apksDir.mkdirs()
        def apks = new File(apksDir, "app-debug-pad.apks")
        if (apks.exists()) apks.delete()
        commandLine getBundletoolCommandLine(["build-apks", "--bundle=${aab.absolutePath}", "--output=${apks.absolutePath}", "--local-testing"])
    }
}

tasks.register("installApksForPad", Exec) {
    description = "Install APKs from buildApksForPad (PAD asset pack included)"
    group = "install"
    dependsOn tasks.named("buildApksForPad")
    doFirst {
        def buildDir = layout.buildDirectory.get().asFile
        def apks = file("${buildDir}/outputs/apks/app-debug-pad.apks")
        if (!apks.exists()) throw new RuntimeException("APKs not found. Run buildApksForPad first.")
        commandLine getBundletoolCommandLine(["install-apks", "--apks=${apks.absolutePath}"])
    }
}

tasks.register("installDebugWithPad") {
    description = "Build debug AAB, generate APKs with --local-testing via bundletool, install on device (PAD enabled)"
    group = "install"
    dependsOn tasks.named("installApksForPad")
    doLast {
        println "Installed debug build with PAD (asset pack). getAssetPackPath(\"sherpa_models\") will now return the pack path."
    }
}
